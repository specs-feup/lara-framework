plugins {
    id 'distribution'
    id 'java' // Java project
    id 'jacoco'
}

java {
    withSourcesJar()

    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

// Repositories providers
repositories {
    mavenCentral()
}

dependencies {
    implementation ':SpecsUtils'
    implementation ':tdrcLibrary'
    implementation ':LaraUtils'

    implementation group: 'org.apache.commons', name: 'commons-text', version: '1.13.0'

    // Testing dependencies (matching LaraUtils)
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter', version: '5.10.0'
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '5.5.0'
    testImplementation group: 'org.mockito', name: 'mockito-junit-jupiter', version: '5.5.0'
    testImplementation group: 'org.assertj', name: 'assertj-core', version: '3.24.2'
    testImplementation group: 'org.mockito', name: 'mockito-inline', version: '5.2.0' // For static mocking
    testRuntimeOnly group: 'org.junit.platform', name: 'junit-platform-launcher'
}

// Project sources
sourceSets {
    main {
        java {
            srcDir 'src'
        }
        resources {
            srcDir 'resources'
        }
    }

    test {
        java {
            srcDir 'test'
        }
        resources {
            srcDir 'resources'
            srcDir 'test-resources'
        }
    }
}

// Test coverage configuration (matching LaraUtils)
jacocoTestReport {
    reports {
        xml.required = true
        html.required = true
    }

    finalizedBy jacocoTestCoverageVerification
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.80 // Standard minimum coverage target
            }
        }
    }
}

// Make sure jacoco report is generated after tests
test {
    useJUnitPlatform()

    maxParallelForks = Runtime.runtime.availableProcessors()

    finalizedBy jacocoTestReport
}
