plugins {
    id 'distribution'
    id 'java'
    id 'jacoco'
    id 'me.champeau.jmh' version '0.7.2'
}

java {
    withSourcesJar()

    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

// Repositories providers
repositories {
    mavenCentral()
}

dependencies {
    implementation ':jOptions'
    implementation ':SpecsUtils'
    implementation ':tdrcLibrary'
    implementation ':LanguageSpecification'
    implementation ':LaraUtils'

    // Testing dependencies
    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.0'
    testImplementation 'org.mockito:mockito-core:5.5.0'
    testImplementation 'org.mockito:mockito-junit-jupiter:5.5.0'
    testImplementation 'org.assertj:assertj-core:3.24.2'
    testImplementation 'org.mockito:mockito-inline:5.2.0' // For static mocking
    testImplementation 'org.junit-pioneer:junit-pioneer:2.3.0' // For test retries
    testImplementation 'com.github.stefanbirkner:system-lambda:1.2.1'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher:1.10.0'
}

// Project sources
sourceSets {
    main {
        java {
            srcDir 'src'
        }
    }

    test {
        java {
            srcDir 'test'
        }
    }

    jmh {
        java {
            srcDir 'src-jmh'
        }
    }
}

test {
    useJUnitPlatform()

    maxParallelForks = Math.max(1, (int) (Runtime.getRuntime().availableProcessors() / 2))

    finalizedBy(jacocoTestReport)
}


jacocoTestCoverageVerification {
    executionData fileTree(dir: project.buildDir, includes: [
            'jacoco/test.exec',
            'jacoco/test.exec*',
            'jacoco/test/*.exec'
    ])
    sourceSets sourceSets.main
    violationRules {
        // Overall rule
        rule {
            element = 'BUNDLE'
            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.84
            }
        }
        // Package-specific stricter rules
        rule {
            element = 'PACKAGE'
            includes = ['org.lara.interpreter.weaver.events']
            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.95
            }
        }
        rule {
            element = 'PACKAGE'
            includes = ['org.lara.interpreter.weaver.ast']
            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.95
            }
        }
    }
}

jacocoTestReport {
    reports {
        xml.required = true
        html.required = true
    }
    executionData fileTree(dir: project.buildDir, includes: [
            'jacoco/test.exec',
            'jacoco/test.exec*',
            'jacoco/test/*.exec'
    ])
    sourceDirectories.from = sourceSets.main.allSource.srcDirs
    classDirectories.from = sourceSets.main.output.classesDirs

    finalizedBy(jacocoTestCoverageVerification)
}

jmh {
    duplicateClassesStrategy = DuplicatesStrategy.WARN
}
