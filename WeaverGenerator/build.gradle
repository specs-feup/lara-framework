plugins {
    id 'distribution'
    id 'application'
    id 'java'
    id 'jacoco'
    id 'me.champeau.jmh' version '0.7.2'
}

java {
    withSourcesJar()

    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

application {
    mainClass.set("org.lara.interpreter.weaver.generator.commandline.WeaverGenerator")
}

// Repositories providers
repositories {
    mavenCentral()
}

dependencies {
    implementation ':JavaGenerator'
    implementation ':jOptions'
    implementation ':SpecsUtils'
    implementation ':tdrcLibrary'

    implementation ':LanguageSpecification'
    implementation ':LARAI'
    implementation ':LaraUtils'
    implementation ':WeaverInterface'

    implementation 'commons-cli:commons-cli:1.9.0'

    // Testing dependencies
    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.0'
    testImplementation 'org.mockito:mockito-core:5.5.0'
    testImplementation 'org.mockito:mockito-junit-jupiter:5.5.0'
    testImplementation 'org.assertj:assertj-core:3.24.2'
    testImplementation 'org.mockito:mockito-inline:5.2.0' // For static mocking
    testImplementation 'org.junit-pioneer:junit-pioneer:2.3.0' // For test retries
    testImplementation 'com.github.stefanbirkner:system-lambda:1.2.1'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher:1.10.0'
}

// Project sources
sourceSets {
    main {
        java {
            srcDir 'src'
        }

        resources {
            srcDir 'resources'
        }
    }

    test {
        java {
            srcDir 'test'
        }
        resources {
            srcDir 'test-resources'
        }
    }

    jmh {
        java {
            srcDir 'src-jmh'
        }
    }
}

test {
    useJUnitPlatform()

    // Use up to half the available processors, minimum 1
    maxParallelForks = Math.max(1, (int) (Runtime.getRuntime().availableProcessors() / 2))

    finalizedBy(jacocoTestReport)
}


jacocoTestCoverageVerification {
    // Exclude non-core/entrypoint UI and CLI packages, templated internals, and low-signal utils from coverage accounting
    def excludes = [
        '**/gui/**',
        '**/generator/generator/templated/**'
    ]
    def mainOutput = files(sourceSets.main.output.classesDirs)
    classDirectories.setFrom(mainOutput.asFileTree.matching { exclude excludes })

    executionData fileTree(dir: project.buildDir, includes: [
            'jacoco/test.exec',
            'jacoco/test.exec*',
            'jacoco/test/*.exec'
    ])

    violationRules {
        // Overall rule
        rule {
            element = 'BUNDLE'
            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.73
            }
        }
        // Package-specific stricter rules for parsing/validation and code generation
        rule {
            element = 'PACKAGE'
            includes = ['org.lara.interpreter.weaver.generator.*']
            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.58
            }
        }
    }
}

jacocoTestReport {
    reports {
        xml.required = true
        html.required = true
    }
    executionData fileTree(dir: project.buildDir, includes: [
            'jacoco/test.exec',
            'jacoco/test.exec*',
            'jacoco/test/*.exec'
    ])
    sourceDirectories.from = sourceSets.main.allSource.srcDirs
    classDirectories.from = sourceSets.main.output.classesDirs

    finalizedBy(jacocoTestCoverageVerification)
}

jmh {
    duplicateClassesStrategy = DuplicatesStrategy.WARN
}
