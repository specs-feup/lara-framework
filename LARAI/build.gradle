plugins {
    id 'distribution'
    id 'java'
    id 'jacoco'
}

java {
    withSourcesJar()

    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

// Repositories providers
repositories {
    mavenCentral()
}

dependencies {
    implementation ':jOptions'
    implementation ':SpecsUtils'

    implementation ':LaraUtils'
    implementation ':LanguageSpecification'
    implementation ':WeaverInterface'

    implementation group: 'commons-cli', name: 'commons-cli', version: '1.9.0'
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.12.1'

    // Testing dependencies
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter', version: '5.10.0'
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '5.5.0'
    testImplementation group: 'org.mockito', name: 'mockito-junit-jupiter', version: '5.5.0'
    testImplementation group: 'org.assertj', name: 'assertj-core', version: '3.24.2'
    testImplementation group: 'org.mockito', name: 'mockito-inline', version: '5.2.0' // For static mocking
    testRuntimeOnly group: 'org.junit.platform', name: 'junit-platform-launcher'
}

// Project sources
sourceSets {
    main {
        java {
            srcDir 'src'
        }
        resources {
            srcDir 'resources'
        }
    }
    test {
        java {
            srcDir 'test'
        }
        resources {
            srcDir 'test-resources'
        }
    }
}

// Test coverage configuration
jacocoTestReport {
    reports {
        xml.required = true
        html.required = true
    }

    finalizedBy jacocoTestCoverageVerification
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.80 // 80% minimum coverage
            }
        }
    }
}

// Make sure jacoco report is generated after tests
test {
    useJUnitPlatform()

    maxParallelForks = Runtime.runtime.availableProcessors()

    finalizedBy jacocoTestReport
}

task json(type: JavaExec) {
    group = "Execution"
    description = "Generates the LaraJoinPointSpecification.json file for Lara-JS"
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'org.lara.interpreter.LaraJoinPointJsonGeneratorLauncher'
    args = []
}
